---
layout: page
title:  "Cross-Lagged Panel Model"
subtitle: ""
date:   2024-02-24 10:45:01 +0530
categories: ["Longitudinal Analysis"]
---


<h2><strong>What is a Cross-Lagged Panel Model (CLPM)?</strong></h2>

<p>Cross-lagged panel analysis is an analytical strategy used to describe reciprocal relationships, or directional influences, between variables over time (Kearney, 2017). The Cross-Lagged Panel Model (CLPM) has been widely used in behavioral and psychological research (Usami et al., 2019).</p>

<p>The simplest form of a CLPM is shown in the figure below, where two variables are measured at two different time points. This model includes two X variables (x1, x2) and two Y variables (y1, y2).</p>

<img src="{{ '/assets/img/20240123/640.png' | prepend: site.baseurl }}" id="about-img" style="width: 30%; max-width: 800px;">

<h2><strong>CLPM is Mainly Used to Assess Three Types of Relationships</strong></h2>

<ol>
  <li><strong>Synchronous Correlations</strong>: Refers to the relationship between two variables at the same time point. In the figure above, these are the correlations between x1 and y1 (r_x1y1), and x2 and y2 (r_x2y2).</li>
  <li><strong>Autoregressive Effects</strong>: Refers to the relationship between the same variable at different time points. In the figure above, this is the effect from x1 to x2 (β3) and from y1 to y2 (β4).
    <p><i>Autoregressive effects describe the stability of constructs over time. Smaller autoregressive coefficients (closer to zero) suggest more variance in the construct, meaning less stability or influence from the previous time point. Larger autoregressive coefficients indicate less variance, meaning greater stability or influence from the previous time point (Kearney, 2017).</i></p>
  </li>
  <li><strong>Cross-Lagged Effects</strong>: Refers to the relationship between different variables at different time points. In the figure above, this is the effect from x1 to y2 (β1), and from y1 to x2 (β2).</li>
</ol>

<h2><strong>Advantages and Disadvantages of CLPM</strong></h2>

<p>The advantage of CLPM is that it allows for the examination of how one variable predicts another, while controlling for the correlation between variables at the same time point and the stability of variables over time.</p>

<p>However, a major limitation of CLPM is that it does not separate between-person effects from within-person effects. Longitudinal data are inherently multilevel, where the sources of variance can be categorized into between-person and within-person levels. But CLPM confounds these two distinct levels of effects.</p>

<p>A significant limitation of CLPM is that depending on the variance structures of between-person and within-person effects, the results may reflect predominantly between-person effects, within-person effects, or a confusing blend of both, leaving researchers with uninterpretable findings (Masselink et al., 2018).</p>

<p>To address this limitation, Hamaker et al. (2015) introduced the Random Intercepts Cross-Lagged Panel Model (RI-CLPM), which I will discuss in my next article.</p>

<h2><strong> How to Perform CLPM in Mplus</strong></h2>

<h3><strong>1. Observed Variable Model – Two Variables, Two Waves</strong></h3>
<p>Suppose there are two variables, X and Y, measured at two time points, resulting in four variables: x1, x2, y1, y2. The corresponding Mplus code for CLPM is:</p>

<pre>
TITLE: Cross-Lagged Panel Model, 2 variables, 2 waves;

DATA: FILE IS 123.dat; ! Data source

VARIABLE: NAME ARE x1 x2 y1 y2; ! Variable names
          MISSING=ALL(99); ! Define missing values
          USEVARIABLES ARE x1 x2 y1 y2; ! Variables to use

ANALYSIS: ESTIMATOR=MLR; ! Estimation method, select based on data characteristics

MODEL: x2 y2 ON x1; ! x1 to x2 and y2
       x2 y2 ON y1; ! y1 to x2 and y2
       x1 WITH y1; ! Correlation between x1 and y1 at the same time point
       x2 WITH y2;

OUTPUT: SAMPSTAT STDYX MOD CINTERVAL; ! Output sample statistics, standardized values, modification indices, confidence intervals, etc.
</pre>

<h3><strong>2. Observed Variable Model – Three Variables, Three Waves</strong></h3>
<p>Suppose X is the independent variable, M is the mediator, and Y is the dependent variable. Each variable is measured three times, resulting in nine variables: x1, x2, x3, m1, m2, m3, y1, y2, y3. The corresponding Mplus code for CLPM is:</p>

<pre>
TITLE: Cross-Lagged Panel Model, 3 variables, 3 waves;

DATA: FILE IS 123.dat; ! Data source

VARIABLE: NAME ARE x1 x2 x3 m1 m2 m3 y1 y2 y3; ! Variable names
          MISSING=ALL(99); ! Define missing values
          USEVARIABLES ARE x1 x2 x3 m1 m2 m3 y1 y2 y3; ! Variables to use

ANALYSIS: ESTIMATOR=MLR; ! Estimation method, select based on data characteristics

MODEL: x2 m2 y2 ON x1; 
       x2 m2 y2 ON m1;
       x2 m2 y2 ON y1;
       x3 m3 y3 ON x2;
       x3 m3 y3 ON m2;
       x3 m3 y3 ON y2;
       x1 WITH m1 y1;
       m1 WITH y1;
       x2 WITH m2 y2;
       m2 WITH y2;
       x3 WITH m3 y3;
       m3 WITH y3;

OUTPUT: SAMPSTAT STDYX MOD CINTERVAL;
</pre>

<p>You can also test longitudinal mediation effects with this model:</p>

<pre>
TITLE: CROSS-LAGGED PANEL MODEL;

DATA: FILE IS 123.dat; ! Data source

VARIABLE: NAME ARE x1 x2 x3 m1 m2 m3 y1 y2 y3; ! Variable names
          MISSING=ALL(99); ! Define missing values
          USEVARIABLES ARE x1 x2 x3 m1 m2 m3 y1 y2 y3; ! Variables to use

ANALYSIS: BOOTSTRAP=2000; ! Use Bootstrap method for mediation effect testing, with 1000, 2000, or 5000 resamples (based on literature)

MODEL: x2 m2 y2 ON x1;
       x2 m2 y2 ON m1;
       x2 m2 y2 ON y1;
       x3 m3 y3 ON x2;
       x3 m3 y3 ON m2;
       x3 m3 y3 ON y2;
       x1 WITH m1 y1;
       m1 WITH y1;
       x2 WITH m2 y2;
       m2 WITH y2;
       x3 WITH m3 y3;
       m3 WITH y3;

MODEL INDIRECT: y3 IND m2 x1;

OUTPUT: SAMPSTAT STDYX CINTERVAL;
</pre>

<h3><strong>3. Observed Variable Model – Three Variables, Two Waves</strong></h3>
<p>Suppose X is the independent variable, M is the mediator, and Y is the dependent variable. Each variable is measured twice, resulting in six variables: x1, x2, m1, m2, y1, y2. The corresponding Mplus code for CLPM is:</p>

<pre>
TITLE: Cross-Lagged Panel Model, 3 variables, 2 waves;

DATA: FILE IS 12.dat; ! Data source

VARIABLE: NAME ARE x1 x2 m1 m2 y1 y2; ! Variable names
          MISSING=ALL(99); ! Define missing values
          USEVARIABLES ARE x1 x2 m1 m2 y1 y2; ! Variables to use

ANALYSIS: ESTIMATOR=MLR; ! Estimation method

MODEL: x2 m2 y2 ON x1;
        x2 m2 y2 ON m1;
        x2 m2 y2 ON y1;
        x1 WITH m1 y1;
        m1 WITH y1;
        x2 WITH m2 y2;
        m2 WITH y2;

OUTPUT: SAMPSTAT STDYX MOD CINTERVAL;
</pre>

<h3><strong>4. Latent Variable CLPM</strong></h3>
<p>In contrast to the observed variable model, a latent variable model creates latent variables using <code>BY</code>. For two variables and two waves of data, the Mplus syntax for a latent variable model is:</p>

<pre>
TITLE: Cross-Lagged Panel Model, 2 variables, 2 waves;

DATA: FILE IS 123.dat; ! Data source

VARIABLE: NAME ARE A1-A4 A5-A8 B1-B4 B5-B8; ! Variable names
          MISSING=ALL(99); ! Define missing values
          USEVARIABLES ARE A1-A4 A5-A8 B1-B4 B5-B8; ! Variables to use

ANALYSIS: ESTIMATOR=MLR; ! Estimation method, select based on data characteristics

MODEL: x1 BY A1-A4;
       x2 BY A5-A8;
       y1 BY B1-B4;
       y2 BY B5-B8; ! Create latent variables
       x2 y2 ON x1; ! x1 to x2 and y2
       x2 y2 ON y1; ! y1 to x2 and y2
       x1 WITH y1; ! Correlation between x1 and y1 at the same time point
       x2 WITH y2;

OUTPUT: SAMPSTAT STDYX MOD CINTERVAL;
</pre>

<h3><strong>5. How to Add Covariates in CLPM</strong></h3>
<p>For categorical variables such as gender, you can code females as 0 and males as 1, and then control for gender by using the statement: <code>research variable ON gender</code>. For continuous variables, you can directly control by using the statement: <code>research variable ON control variable</code>. Below is the Mplus syntax for adding the covariate "gender" to the Cross-Lagged Panel Model:</p>

<pre>
TITLE: Cross-Lagged Panel Model, 2 variables, 2 waves;

DATA: FILE IS 12.dat;

VARIABLE: NAMES ARE gender y1 y2 x1 x2;
          MISSING ARE ALL (-99);
          USEVARIABLES = gender y1 y2 x1 x2;

MODEL: x2 ON x1 y1;
       y2 ON y1 x1;
       x1 WITH y1;
       x2 WITH y2;
       y1 y2 x1 x2 ON gender;

OUTPUT: SAMPSTAT STDYX MOD CINTERVAL;
</pre>

<h2><strong>Further Reading</strong></h2>
<p>For more detailed information on CLPM, I recommend the following articles and books:
<br>Hamaker, E. L., Kuiper, R. M., & Grasman, R. P. P. P. (2015). A critique of the cross-lagged panel model. <i>Psychological Methods</i>, 20(1), 102–116. <a href="https://doi.org/10.1037/a0038889">https://doi.org/10.1037/a0038889</a>
<br>Kearney, M. W. (2017). Cross-lagged panel analysis. In <i>The SAGE encyclopedia of communication research methods</i> (pp. 313-314). SAGE Publications, Inc.
<br>Masselink, M., Van Roekel, E., Hankin, B. L., Keijsers, L., Lodder, G. M. A., Vanhalst, J., Verhagen, M., Young, J. F., & Oldehinkel, A. J. (2018). The Longitudinal Association between Self-esteem and Depressive Symptoms in Adolescents: Separating between-person effects from within-person effects. <i>European journal of personality</i>, 32(6), 653–671. <a href="https://doi.org/10.1002/per.2179">https://doi.org/10.1002/per.2179</a>
<br>Usami, S., Murayama, K., & Hamaker, E. L. (2017). A unified framework of cross-Lagged longitudinal models. In <i>82nd Annual Meeting of Psychometric Society</i>.</p> 
<p>That concludes this blog post. If you have any questions, feel free to reach out!</p> 
